include(CMakeFindDependencyMacro)
find_dependency(OGRE)
set(OGRE_FOUND ((TARGET OgreMain) AND ((TARGET OgreOverlay)) AND ((TARGET OgreGLSupport))))

if(NOT (TARGET rviz_ogre_vendor::OgreMain))
  message(STATUS " Adding rviz_ogre_vendor::OgreMain target ")
  add_library(rviz_ogre_vendor::OgreMain INTERFACE IMPORTED GLOBAL)
  #Find opengl and add it's imported targets
  find_dependency(OpenGL COMPONENTS OpenGL)
  #There's no imported target for DL, so use a _LIBS variable like an unwashed barbarian
  set(ogremaindeps "${CMAKE_DL_LIBS};OgreMain;OpenGL::GL")
  if(TARGET OpenGL::EGL)
    list(APPEND ogremaindeps OpenGL::EGL)
  endif()
  set_target_properties(rviz_ogre_vendor::OgreMain 
    PROPERTIES 
      INTERFACE_LINK_LIBRARIES 
        "${ogremaindeps}"
  )
endif()

if(NOT (TARGET rviz_ogre_vendor::OgreOverlay))
  message(STATUS " Adding rviz_ogre_vendor::OgreOverlay target ")
  add_library(rviz_ogre_vendor::OgreOverlay INTERFACE IMPORTED GLOBAL)
  set_target_properties(rviz_ogre_vendor::OgreOverlay 
    PROPERTIES 
      INTERFACE_LINK_LIBRARIES OgreOverlay
  )
endif()

if(NOT (TARGET rviz_ogre_vendor::OgreGLSupport))
  message(STATUS " Adding rviz_ogre_vendor::OgreGLSupport target ")
  add_library(rviz_ogre_vendor::OgreGLSupport INTERFACE IMPORTED GLOBAL)
  set_target_properties(rviz_ogre_vendor::OgreGLSupport 
    PROPERTIES 
      INTERFACE_LINK_LIBRARIES OgreGLSupport
  )
endif()